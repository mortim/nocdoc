load seq
load str
load std:string
load std:stack
load nocdoc/include/utils.noc
load nocdoc/include/constants.noc

def arrow_icon_html = {
    "<i class=\"fa-solid fa-arrow-right white-color\"></i>"
}

def formatHTML = {
    ---
    source: <a href="https://github.com/noc-lang/nocdoc/blob/main/render_html.noc#L12">nocdoc/render_html.noc#formatHTML</a>

    Format some website blocks in HTML

    Syntax: [const] formatHTML => "html_content"
        consts:
            - <a href="constants.html#[TABLE_OF_CONTENTS_SIDEBAR]">TABLE_OF_CONTENTS_SIDEBAR</a>
            - <a href="constants.html#[TABLE_OF_CONTENTS_LINKS]">TABLE_OF_CONTENTS_LINKS</a>
            - <a href="constants.html#[DETAILS_SIDEBAR]">DETAILS_SIDEBAR</a>
            - <a href="constants.html#[FUNCTIONS_SIDEBAR]">FUNCTIONS_SIDEBAR</a>
            - <a href="constants.html#[NOC_FUNCTIONS_DOCUMENTATION]">NOC_FUNCTIONS_DOCUMENTATION</a>
            - <a href="constants.html#[PAGE]">PAGE</a>
    ---

    [
        [[TABLE_OF_CONTENTS_SIDEBAR] [
                    # example => table_of_contents : [["section1" ["link1.html" "link2.html"]] ["section2" ["link3.html" "link4.html"]]]
                    #   -> first iteration: "acc" ["section1" ["link1.html" "link2.html"]]

                    "" [ # "acc" "section1" ["link1.html" "link2.html"]
                         unquote
                         # "acc" "section1" ["table_of_contents_html"]
                         TABLE_OF_CONTENTS_LINKS formatHTML quote
                         # "acc" ["section1" "table_of_contents_html"]
                         quoteTwoElems
                         # "acc" "<li>(arrow_icon_html) section1<ul>table_of_contents_html</ul></li>"
                         "<li>" arrow_icon_html cat " <span class=\"white-color base-font\">{}</span><ul>{}</ul></li>" cat swap format
                         # "acc<li>(arrow_icon_html) <span class=\"white-color\">section1</span><ul>table_of_contents_html</ul></li>"
                         cat
                       ] fold
        ]]

        [[TABLE_OF_CONTENTS_LINKS] [
                # example => links: ["link1.html" "link2.html"]
                #   -> first iteration: "acc" "link1.html"

                "" [ # "acc" "link1.html" "link1.html"
                     dup
                     # "acc" "link1.html" ["link1"]
                     $ 5 removeNChars tostr quote
                     # "acc" ["link.html" "link1"]
                     quoteTwoElems
                     # "acc" "<li><a href=\"link.html\" class=\"white-color\">link1</a></li>"
                     "<li><a href=\"{}\" class=\"white-color base-font\">{}</a></li>" swap format
                     # "acc<li><a href=\"link1.html\" class=\"white-color\">link1</a></li>"
                     cat
                ] fold
        ]]

        [[DETAILS_SIDEBAR] ["
                    <h3 class=\"white-color base-font\">Details</h3>
                    <li>
                        <b class=\"white-color base-font\"><i class=\"fa-solid fa-at\"></i> Author:</b> <span class=\"base-font white-color\">{}</span></li><li>
                        <b class=\"white-color base-font\"><i class=\"fa-solid fa-code-branch\"></i> Git repos: </b><a href=\"{}\" class=\"white-color base-font\">{}</a>
                    </li>
                    " swap format ]]

        [[NOC_FUNCTIONS_DOCUMENTATION] [
            # example => functions : [[func_a] [func_b]]
            #   -> first iteration: "acc" [func_a]

            "" [ # "acc" "[func_a]" "[func_a]" "func_a_doc"
                dup dup str swap str 3 -1 rotNM help
                # "acc" "[func_a]" "[func_a]" ["func_a_doc_with_br_tags"]
                $ [] swap replaceNewLines tostr quote
                # "acc" ["[func_a]" "[func_a]" "func_a_doc_with_br_tags"]
                quoteTwoElems quoteTwoElems
                # "acc" "<div id=\"#[func_a]\"><h3>[func_a]</h3>func_a_doc_with_br_tags</div>"
                "<div id=\"{}\"><h3>{}</h3>{}</div>" swap format
                # "acc<div id=\"#[func_a]\"><h3>[func_a]</h3>func_a_doc_with_br_tags</div>"
                cat
            ] fold

            # ["noc_functions_html"]
            quote

            # "<div id=\"functions\">noc_functions_html</div>"
            "<div id=\"functions\">{}</div>" swap format
        ]]

        [[FUNCTIONS_SIDEBAR] [
            # example => functions : [[func_a] [func_b]]
            #   -> first iteration: "acc" [func_a]

            "" [ # "acc" "[func_a]" ["[func_a]"]
                 str dup quote
                 # "acc" ["[func_a]" "[func_a]"]
                 quoteTwoElems
                 # "acc" "<li><a href=\"#[func_a]\" class=\"white-color\">[func_a]</a></li>"
                 "<li class=\"functions-name\"><a href=\"#{}\" class=\"white-color base-font\">{}</a></li>" swap format
                 # "acc<li><a href=\"#[func_a]\" class=\"white-color\">[func_a]</a></li>"
                 cat
                ] fold 

            "<h3 class=\"white-color base-font\">Functions</h3>" swap cat
        ]]

        [[PAGE] [ # ["name_page" "page_type" "name_page" "table_of_contents_html" "sidebar2_html" "content"]
                  quote quoteTwoElems quoteTwoElems quoteTwoElems quoteTwoElems quoteTwoElems 
                  # "html_content"
                  "nocdoc/template.html" read swap format
        ]]

    ] case
}

def renderHomePage = {
    ---
    source: <a href="https://github.com/noc-lang/nocdoc/blob/main/render_html.noc#L120">nocdoc/render_html.noc#renderHomePage</a>

    Render the homepage in HTML format string

    Syntax: info renderHomePage => "name.html" "html_content"
        info -> ["name" "author" [desc] "url_git" table_of_contents]
    ---

    # "name" "author" [desc] "url_git" table_of_contents
    unquote

    # "name" "author" [desc] "url_git" "table_of_contents_html"
    TABLE_OF_CONTENTS_SIDEBAR formatHTML

    # "author" [desc] "url_git" "table_of_contents_html" "name" "name"
    5 -1 rotNM dup

    # "author" [desc] "url_git" "table_of_contents_html" "name.html" "name"
    ".html" cat swap 

    # "name.html" "name" "author" [desc] "url_git" "table_of_contents_html"
    6 2 rotNM 

    # "name.html" "name" "table_of_contents_html" "author" [desc] "url_git" 
    4 1 rotNM

    # "name.html" "name" "table_of_contents_html" [desc] "author" "url_git" ["url_git"]
    3 -1 rotNM swap dup quote

    # "name.html" "name" "table_of_contents_html" [desc] ["author" "url_git" "url_git"]
    quoteTwoElems quoteTwoElems

    # "name.html" "name" "table_of_contents_html" [desc] "sidebar2_html" 
    DETAILS_SIDEBAR formatHTML

    # "name.html" "name" "table_of_contents_html" "sidebar2_html" "desc"
    swap unquote

    # "name.html" "table_of_contents_html" "sidebar2_html" "desc" "name" "home" "name"
    4 -1 rotNM dup "home" swap

    # "name.html" "name" "home" "name" "table_of_contents_html" "sidebar2_html" "desc"
    6 3 rotNM

    # "name.html" "html_content"
    PAGE formatHTML
}

def renderModule = {
    ---
    source: <a href="https://github.com/noc-lang/nocdoc/blob/main/render_html.noc#L170">nocdoc/render_html.noc#renderModule</a>

    Render the Noc module in HTML format string

    Syntax: info functions "module_name" renderModule => "module_name.html" "html_content"
        info -> ["name" "author" [desc] "url_git" table_of_contents]
        functions -> [[func_a] [func_b] ...]
    ---

    # functions "module_name" "name" "author" [desc] "url_git" table_of_contents
    3 -1 rotNM unquote

    # functions "module_name" "table_of_contents_html"
    TABLE_OF_CONTENTS_SIDEBAR formatHTML

    # functions "module_name" "table_of_contents_html"
    swap pop
    swap pop
    swap pop
    swap pop

    # "module_name" "table_of_contents_html" functions functions
    3 -1 rotNM dup

    # "module_name" "table_of_contents_html" functions "functions_html"
    NOC_FUNCTIONS_DOCUMENTATION formatHTML

    # "module_name" "table_of_contents_html" "functions_sidebar" "functions_html" 
    swap FUNCTIONS_SIDEBAR formatHTML swap

    # "module_name.html" "module_name" "table_of_contents_html" "functions_sidebar" "functions_html" 
    4 -1 rotNM dup ".html" cat swap 5 2 rotNM

    # "module_name.html" "table_of_contents_html" "functions_sidebar" "functions_html" "module_name" "module" "module_name"
    4 -1 rotNM dup "module" swap

    # "module_name.html" "module_name" "module" "module_name" "table_of_contents_html" "functions_sidebar" "functions_html"
    6 3 rotNM

    # "module_name.html" "html_content"
    PAGE formatHTML
}

def renderWikiPage = {
    ---
    source: <a href="https://github.com/noc-lang/nocdoc/blob/main/render_html.noc#L215">nocdoc/render_html.noc#renderWikiPage</a>

    Render the Noc wiki in HTML format string

    Syntax: wikiInfo wikiPage "wiki_page_name" renderModule => "wiki_page_name.html" "html_content"
        wikiInfo -> ["name" "author" "desc" "url_git" table_of_contents]
        wikiPage -> content page in HTML
    ---

    # wikiPage "wiki_page_name" "name" "author" "desc" "url_git" table_of_contents
    3 -1 rotNM unquote

    # wikiPage "wiki_page_name" "name" "author" "url_git" table_of_contents
    3 -1 rotNM pop

    # wikiPage "wiki_page_name" "name" "author" "url_git" "table_of_contents_html"
    TABLE_OF_CONTENTS_SIDEBAR formatHTML

    # wikiPage "wiki_page_name" "name" "table_of_contents_html" "author" "url_git" ["url_git"] 
    3 1 rotNM dup quote

    # wikiPage "wiki_page_name" "name" "table_of_contents_html" ["author" "url_git" "url_git"]
    quoteTwoElems quoteTwoElems

    # wikiPage "wiki_page_name" "name" "table_of_contents_html" "sidebar2_html"
    DETAILS_SIDEBAR formatHTML

    # wikiPage "wiki_page_name" "table_of_contents_html" "sidebar2_html"
    3 -1 rotNM pop

    # "wiki_page_name.html" wikiPage "table_of_contents_html" "sidebar2_html" "wiki_page_name" 
    3 -1 rotNM dup ".html" cat 5 1 rotNM

    # "wiki_page_name.html" wikiPage "table_of_contents_html" "sidebar2_html" "wiki_page_name" "wiki" "wiki_page_name"
    dup "wiki" swap

    # "wiki_page_name.html" wikiPage "wiki_page_name" "wiki" "wiki_page_name" "table_of_contents_html" "sidebar2_html"
    5 3 rotNM

    # "wiki_page_name.html" "wiki_page_name" "wiki" "wiki_page_name" "table_of_contents_html" "sidebar2_html" wikiPage
    6 -1 rotNM

    # "wiki_page_name.html" "html_content"
    PAGE formatHTML
}